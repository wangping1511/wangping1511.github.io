---
layout: article
title: 发行以太坊代币
tags: ["区块链"]
---

### 1.浏览器安装 MetaMask 插件 [下载地址](https://metamask.io/download.html)
- 注册使用 MetaMask
  
    创建钱包、同意协议、保存密语、验证密语
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734bcc9b4750683?w=1690&h=1158&f=png&s=220076)
    创建成功
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734bd47f46e1bc4?w=1986&h=1554&f=png&s=170192)

- 测试环境存入 ETH

    选择测试网络
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734bfe6795e1211?w=1974&h=1542&f=png&s=211883)
    点击 **存入**  **获取 Ether**
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734c002db7b5fcb?w=1998&h=1546&f=png&s=197681)

    点击获取按钮获取 ether,稍后在钱包查看 ETH 变为 1
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734c0cd6922c01f?w=2388&h=1230&f=png&s=137312)
    ![](https://user-gold-cdn.xitu.io/2020/7/14/1734c3580927accd?w=814&h=1300&f=png&s=72018)

    [获取测试 ETH](https://faucet.ropsten.be/ ) 这个网站也可以获取测试 ETH

### 2.运行合约,发行代币

- 合约代码

```
pragma solidity ^0.4.19;

interface tokenRecipient { function receiveApproval(address _from, uint256 _value, address _token, bytes _extraData) public; }

contract TokenERC20 {
    string public name;
    string public symbol;
    uint8 public decimals = 18;  // decimals 可以有的小数点个数，最小的代币单位。18 是建议的默认值
    uint256 public totalSupply;

    // 用mapping保存每个地址对应的余额
    mapping (address => uint256) public balanceOf;
    // 存储对账号的控制
    mapping (address => mapping (address => uint256)) public allowance;

    // 事件，用来通知客户端交易发生
    event Transfer(address indexed from, address indexed to, uint256 value);

    // 事件，用来通知客户端代币被消费
    event Burn(address indexed from, uint256 value);

    /**
     * 初始化构造
     */
    function TokenERC20(uint256 initialSupply, string tokenName, string tokenSymbol) public {
        totalSupply = initialSupply * 10 ** uint256(decimals);  // 供应的份额，份额跟最小的代币单位有关，份额 = 币数 * 10 ** decimals。
        balanceOf[msg.sender] = totalSupply;                // 创建者拥有所有的代币
        name = tokenName;                                   // 代币名称
        symbol = tokenSymbol;                               // 代币符号
    }

    /**
     * 代币交易转移的内部实现
     */
    function _transfer(address _from, address _to, uint _value) internal {
        // 确保目标地址不为0x0，因为0x0地址代表销毁
        require(_to != 0x0);
        // 检查发送者余额
        require(balanceOf[_from] >= _value);
        // 确保转移为正数个
        require(balanceOf[_to] + _value > balanceOf[_to]);

        // 以下用来检查交易，
        uint previousBalances = balanceOf[_from] + balanceOf[_to];
        // Subtract from the sender
        balanceOf[_from] -= _value;
        // Add the same to the recipient
        balanceOf[_to] += _value;
        Transfer(_from, _to, _value);

        // 用assert来检查代码逻辑。
        assert(balanceOf[_from] + balanceOf[_to] == previousBalances);
    }

    /**
     *  代币交易转移
     * 从自己（创建交易者）账号发送`_value`个代币到 `_to`账号
     *
     * @param _to 接收者地址
     * @param _value 转移数额
     */
    function transfer(address _to, uint256 _value) public {
        _transfer(msg.sender, _to, _value);
    }

    /**
     * 账号之间代币交易转移
     * @param _from 发送者地址
     * @param _to 接收者地址
     * @param _value 转移数额
     */
    function transferFrom(address _from, address _to, uint256 _value) public returns (bool success) {
        require(_value <= allowance[_from][msg.sender]);     // Check allowance
        allowance[_from][msg.sender] -= _value;
        _transfer(_from, _to, _value);
        return true;
    }

    /**
     * 设置某个地址（合约）可以创建交易者名义花费的代币数。
     *
     * 允许发送者`_spender` 花费不多于 `_value` 个代币
     *
     * @param _spender The address authorized to spend
     * @param _value the max amount they can spend
     */
    function approve(address _spender, uint256 _value) public
        returns (bool success) {
        allowance[msg.sender][_spender] = _value;
        return true;
    }

    /**
     * 设置允许一个地址（合约）以我（创建交易者）的名义可最多花费的代币数。
     *
     * @param _spender 被授权的地址（合约）
     * @param _value 最大可花费代币数
     * @param _extraData 发送给合约的附加数据
     */
    function approveAndCall(address _spender, uint256 _value, bytes _extraData)
        public
        returns (bool success) {
        tokenRecipient spender = tokenRecipient(_spender);
        if (approve(_spender, _value)) {
            // 通知合约
            spender.receiveApproval(msg.sender, _value, this, _extraData);
            return true;
        }
    }

    /**
     * 销毁我（创建交易者）账户中指定个代币
     */
    function burn(uint256 _value) public returns (bool success) {
        require(balanceOf[msg.sender] >= _value);   // Check if the sender has enough
        balanceOf[msg.sender] -= _value;            // Subtract from the sender
        totalSupply -= _value;                      // Updates totalSupply
        Burn(msg.sender, _value);
        return true;
    }

    /**
     * 销毁用户账户中指定个代币
     *
     * Remove `_value` tokens from the system irreversibly on behalf of `_from`.
     *
     * @param _from the address of the sender
     * @param _value the amount of money to burn
     */
    function burnFrom(address _from, uint256 _value) public returns (bool success) {
        require(balanceOf[_from] >= _value);                // Check if the targeted balance is enough
        require(_value <= allowance[_from][msg.sender]);    // Check allowance
        balanceOf[_from] -= _value;                         // Subtract from the targeted balance
        allowance[_from][msg.sender] -= _value;             // Subtract from the sender's allowance
        totalSupply -= _value;                              // Update totalSupply
        Burn(_from, _value);
        return true;
    }
}
```



打开在线编辑器 [Remix](http://remix.ethereum.org/), 创建 `TokenERC20.sol` 文件,将代码复制到文件中

![](https://i.loli.net/2021/02/23/OQAEH3ukUqVsTLy.png)
点击侧边栏第二个按钮、编译文件;会输出几条警告信息,不用管.
![](https://i.loli.net/2021/02/23/Zl7QexoScW3TNiq.png)

点击侧边栏第三个按钮 ,  选择 `Injected Web3` , Deploy    ( 输入发行总量,发行币全称,发行币简称 ) , 点击 Deploy 按钮会拉出 MateMask 的提示框点击确认.

![](https://i.loli.net/2021/02/23/FL3wgWdZA79kz2h.png)
![](https://i.loli.net/2021/02/23/jcxfEeOg8aWFNoJ.png)

打开 MateMask 查看详情 , 复制联系地址

![](https://i.loli.net/2021/02/23/czKPvbLAWwM2nH8.png)

打开 MateMask 菜单, 添加代币, 自定义代币, 粘贴地址

![](https://i.loli.net/2021/02/23/2zlYKN8aAcRVkGL.png)
![](https://i.loli.net/2021/02/23/ReAWn8VZtazQFjv.png)
添加成功! 🎉
![](https://i.loli.net/2021/02/23/1pCodHVKWqBk49v.png)